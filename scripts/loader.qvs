SET ThousandSep=' ';
SET DecimalSep=',';
SET MoneyThousandSep=' ';
SET MoneyDecimalSep=',';
SET MoneyFormat='# ##0,00р.;-# ##0,00р.';
SET TimeFormat='h:mm:ss';
SET DateFormat='DD.MM.YYYY';
SET TimestampFormat='DD.MM.YYYY h:mm:ss[.fff]';
SET MonthNames='янв;фев;мар;апр;май;июн;июл;авг;сен;окт;ноя;дек';
SET DayNames='Пн;Вт;Ср;Чт;Пт;Сб;Вс';
SET LongMonthNames='Январь;Февраль;Март;Апрель;Май;Июнь;Июль;Август;Сентябрь;Октябрь;Ноябрь;Декабрь';
SET LongDayNames='понедельник;вторник;среда;четверг;пятница;суббота;воскресенье';
SET FirstWeekDay=0;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='ru-RU';

 //////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Процедура полной перезагрузки всех данных
//
SUB Full_Reload
  // загружаем свежие данные об остатках.
remains:
  LOAD 
     hash128([Склад], [ГодМесяц])   AS remains_key
    ,[Склад]
    ,[ГодМесяц]
    ,[Остатки на конец месяца, шт]  AS [Остаток]
    FROM
      [raw_source\Данные по остаткам.xlsx] (OOXML, EMBEDDED LABELS, TABLE IS Sheet1)
  ;

years:
  LOAD year(fieldValue('ГодМесяц', recNo())) AS [Год] AUTOGENERATE fieldValueCount('ГодМесяц');

  // сохраняем полученный массив данных в qvd нарезая по годам
  FOR i = 1 TO FieldValueCount('Год')
    LET v_suffix = text(FieldValue('Год', i));

segment:
    NOCONCATENATE LOAD * RESIDENT remains WHERE year([ГодМесяц]) = FieldValue('Год', $(i));

    STORE segment INTO [data\remains_$(v_suffix).qvd] (QVD);
    DROP TABLE segment;
  NEXT i;

  DROP TABLES remains, years;

  // загружаем свежие данные о продажах агрегируя их помесячно
sales:
  LOAD 
     hash128([Склад], monthStart([Дата])) AS sales_key
    ,[Склад]
    ,monthStart([Дата])                   AS [ГодМесяц]
    ,sum([Продажи рублей])                AS [Продажи]
    FROM
      [raw_source\Данные по продажам.xls] (BIFF, EMBEDDED LABELS, TABLE IS Sheet1$)
    GROUP BY
       [Склад] 
      ,monthStart([Дата])
  ;

years:
  LOAD year(fieldValue('ГодМесяц', recNo())) AS [Год] AUTOGENERATE fieldValueCount('ГодМесяц');

  // сохраняем полученный массив данных в qvd нарезая по годам
  FOR i = 1 TO FieldValueCount('Год')
    LET v_suffix = text(FieldValue('Год', i));

segment:
    NOCONCATENATE LOAD * RESIDENT sales WHERE year([ГодМесяц]) = FieldValue('Год', $(i));

    STORE segment INTO [data\sales_$(v_suffix).qvd] (QVD);
    DROP TABLE segment;
  NEXT i;

  DROP TABLES sales, years;
ENDSUB

 //////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Процедура инкрементальной загрузки данных
//
SUB Incremental_Load
  // загружаем свежие данные об остатках.
remains:
  LOAD 
     hash128([Склад], [ГодМесяц])   AS remains_key
    ,[Склад]                        AS [Склад]
    ,[ГодМесяц]
    ,[Остатки на конец месяца, шт]  AS [Остаток]
    FROM
      [raw_source\Данные по остаткам.xlsx] (OOXML, EMBEDDED LABELS, TABLE IS Sheet1)
    WHERE
      [ГодМесяц] >= monthStart('$(v_last_load)')
  ;

  IF fieldValueCount('remains_key') > 0 THEN
years:
    NOCONCATENATE LOAD year(fieldValue('ГодМесяц', recNo())) AS [Год] AUTOGENERATE fieldValueCount('ГодМесяц');
        
    FOR i = 1 TO FieldValueCount('Год')
      LET v_suffix = text(FieldValue('Год', i));
      
      // загружаем из QVD старые данные не требующие обновления.
      CONCATENATE (remains) LOAD * FROM [data\remains_$(v_suffix).qvd] (QVD) WHERE NOT exists(remains_key);

segment:
      NOCONCATENATE LOAD * RESIDENT remains WHERE year([ГодМесяц]) = FieldValue('Год', $(i));

      STORE segment INTO [data\remains_$(v_suffix).qvd] (QVD);
      DROP TABLE segment;
    NEXT i;

    DROP TABLE years;
  END IF;

  DROP TABLE remains;

  // загружаем свежие данные о продажах.
sales:
  LOAD 
     hash128([Склад], monthStart([Дата])) AS sales_key
    ,[Склад]
    ,monthStart([Дата])                   AS [ГодМесяц]
    ,sum([Продажи рублей])                AS [Продажи]
    FROM
      [raw_source\Данные по продажам.xls] (BIFF, EMBEDDED LABELS, TABLE IS Sheet1$)
    WHERE
      [Дата] >= monthStart('$(v_last_load)')
    GROUP BY
       [Склад] 
      ,monthStart([Дата])
  ;

  IF fieldValueCount('sales_key') > 0 THEN
years:
    NOCONCATENATE LOAD year(fieldValue('ГодМесяц', recNo())) AS [Год] AUTOGENERATE fieldValueCount('ГодМесяц');
        
    FOR i = 1 TO FieldValueCount('Год')
      LET v_suffix = text(FieldValue('Год', i));
      
    // загружаем из QVD старые данные не требующие обновления.
      CONCATENATE (sales) LOAD * FROM [data\sales_$(v_suffix).qvd] (QVD) WHERE NOT exists(sales_key);
  
segment:
      NOCONCATENATE LOAD * RESIDENT sales WHERE year([ГодМесяц]) = FieldValue('Год', $(i));

      STORE segment INTO [data\sales_$(v_suffix).qvd] (QVD);
      DROP TABLE segment;
    NEXT i;

    DROP TABLE years;
  END IF;

  DROP TABLE sales;
ENDSUB

 //////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Главная
//
SUB Main
  LET v_now = now();

  CALL Full_Reload;
  //CALL Incremental_Load;

  LET v_last_load = v_now;
ENDSUB

CALL Main;