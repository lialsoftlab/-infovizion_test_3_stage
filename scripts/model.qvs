SET ThousandSep=' ';
SET DecimalSep=',';
SET MoneyThousandSep=' ';
SET MoneyDecimalSep=',';
SET MoneyFormat='# ##0,00р.;-# ##0,00р.';
SET TimeFormat='h:mm:ss';
SET DateFormat='DD.MM.YYYY';
SET TimestampFormat='DD.MM.YYYY h:mm:ss[.fff]';
SET MonthNames='янв;фев;мар;апр;май;июн;июл;авг;сен;окт;ноя;дек';
SET DayNames='Пн;Вт;Ср;Чт;Пт;Сб;Вс';
SET LongMonthNames='Январь;Февраль;Март;Апрель;Май;Июнь;Июль;Август;Сентябрь;Октябрь;Ноябрь;Декабрь';
SET LongDayNames='понедельник;вторник;среда;четверг;пятница;суббота;воскресенье';
SET FirstWeekDay=0;
SET BrokenWeeks=1;
SET ReferenceDay=0;
SET FirstMonthOfYear=1;
SET CollationLocale='ru-RU';

facts:
LOAD 
   [Склад]
  ,date([ГодМесяц]) AS [ОтчетнаяДата]
  ,[Остаток]
  FROM [data\remains_*.qvd] (QVD)
;

// сразу нельзя сливать с остатками, при загрузке из нескольких файлов по маске глючит.
sales:
LOAD 
   [Склад]
  ,date([ГодМесяц]) AS [ОтчетнаяДата]
  ,[Продажи]
  FROM [data\sales_*.qvd] (QVD)
;

OUTER JOIN (facts) LOAD * RESIDENT sales;
DROP TABLE sales;

// аналог (с нюансами -- собирает уникальные значения по полю, а не по таблице) 
// LOAD DISTINCT [ОтчетнаяДата] RESIDENT facts, но существенно быстрее.
facts_dates_list:
LOAD fieldValue('ОтчетнаяДата', recNo()) AS [ОтчетнаяДата] AUTOGENERATE FieldValueCount('ОтчетнаяДата');

calendar:
LOAD 
   [ОтчетнаяДата]
  ,date([ОтчетнаяДата], 'YYYY.MM')  AS [ГодМесяц]
  ,dual('thisYear', 0)              AS [Признак]
  RESIDENT 
    facts_dates_list
;


CONCATENATE (calendar) LOAD
   [ГодМесяц]
  ,addYears([ОтчетнаяДата], -iterNo())  AS [ОтчетнаяДата]
  ,dual('year-' & iterNo(), -iterNo())  AS [Признак]
  RESIDENT
    calendar
  WHILE
    iterNo() <= 2
;

// откидываем из календаря строки, по которым нет фактов.
RIGHT JOIN (calendar) LOAD DISTINCT [ОтчетнаяДата] RESIDENT facts_dates_list;
DROP TABLE facts_dates_list;

JOIN (calendar) LOAD
   [ГодМесяц]
  ,year([ГодМесяц])  AS [Год]
  ,month([ГодМесяц]) AS [Месяц]
  RESIDENT
    calendar
;